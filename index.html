<!DOCTYPE html>
<html lang="en">
	<head>
		<title>three.js webgl - performance</title>
		<meta charset="utf-8">
		<meta name="viewport" content="width=device-width, user-scalable=no, minimum-scale=1.0, maximum-scale=1.0">
		<style>
			body {
				background:#fff;
				padding:0;
				margin:0;
				font-weight: bold;
				overflow:hidden;
			}
		</style>

	</head>
	<body>

		<div id="container" style="width: 100%; height: 100%;"></div>
		<script src="/~sijietian/pbr/threejs/build/three.js"></script>
		<script src="/~sijietian/pbr/threejs/controls/OrbitControls.js"></script>
		<script type="text/javascript" src="/~sijietian/pbr/dat.gui.min.js"></script>


		<script id="vertexShader" type="x-shader/x-vertex">

			precision mediump float;
			precision mediump int;

			varying vec3 vPosition;
			varying vec3 vViewPosition;
			varying vec4 vNormal;
			varying vec4 vViewNormal;

			void main()	{

				vPosition = position;
				vViewPosition = (modelViewMatrix * vec4(position,1.0)).xyz;
				vNormal = vec4(normal.xyz, 0.0);
				vViewNormal = modelViewMatrix * vec4(normal.xyz, 0.0);

				gl_Position = projectionMatrix * modelViewMatrix * vec4( position, 1.0 );
			}

		</script>

		<script id="fragmentShader_param" type="x-shader/x-fragment">

			precision mediump float;
			precision mediump int;

			uniform vec3 u_lightColor;
			uniform vec3 u_lightDir;
			uniform vec3 u_lightPos;
			uniform vec3 u_viewPos;
			uniform vec3 u_diffuseColor;
			uniform float u_roughness;
			uniform float u_fresnel;

			varying vec3 vPosition;
			varying vec3 vViewPosition;
			varying vec4 vNormal;
			varying vec4 vViewNormal;

			#define M_PI 3.1415926535897932384626433832795

			float dotClamped(vec3 a, vec3 b) {
				return max(dot(a,b), 0.0);
			}

			float F(float f0, vec3 l, vec3 h) {
				return f0 + (1.0-f0) * pow(1.0-dot(l,h),5.0);
			}

		</script>


		<script id="NDFBlinnPhong" type="x-shader/x-fragment">
			float N(float a, vec3 n, vec3 h) {
				float powTerm = 2.0/(a*a) - 2.0;
				float NdotHClamped = max(dot(n,h), 0.0);
				return (1.0 / (M_PI * a * a)) * (pow(NdotHClamped, powTerm));
			}
		</script>

		<script id="NDFBeckmann" type="x-shader/x-fragment">
			float N(float a, vec3 n, vec3 h) {
				float NdotHClamped = max(dot(n,h), 0.0);
				return 1.0 / (M_PI * a * a * pow(NdotHClamped, 4.0)) * exp((pow(NdotHClamped,2.0) - 1.0) / (a * a * pow(NdotHClamped, 2.0)));
			}
		</script>

		<script id="NDFGGX" type="x-shader/x-fragment">
			float N(float a, vec3 n, vec3 h) {
				float NdotHClamped = max(dot(n,h), 0.0);
				float a2 = a*a;
				return a2 / (M_PI * pow(pow(NdotHClamped, 2.0) * (a2 - 1.0) + 1.0, 2.0));
			}
		</script>

		<script id="GImplicit" type="x-shader/x-fragment">
			float G(float a, vec3 l, vec3 v, vec3 h, vec3 n) {
				return max(dot(l, n), 0.0) * max(dot(v, n), 0.0);
			}
		</script>


		<script id="GCookTorrance" type="x-shader/x-fragment">
			float G(float a, vec3 l, vec3 v, vec3 h, vec3 n) {
				float NdotVClamped = max(dot(n,v), 0.0);
				float NdotLClamped = max(dot(n,l), 0.0);
				float VdotH = max(dot(v,h), 0.0);
				float NdotH = max(dot(n,h), 0.0);
				return min(1.0, min((2.0 * NdotH * NdotVClamped) / VdotH, (2.0 * NdotH * NdotLClamped) / VdotH));
			}
		</script>


		<script id="GKelemen" type="x-shader/x-fragment">
			float G(float a, vec3 l, vec3 v, vec3 h, vec3 n) {
				float NdotLClamped = max(dot(n,l), 0.0);
				float NdotVClamped = max(dot(n,v), 0.0);
				return ( NdotLClamped * NdotVClamped ) / pow(dot(v,h), 2.0);
			}
		</script>


		<script id="GBeckmann" type="x-shader/x-fragment">

			float GBeckmannHelper(float a, vec3 t, vec3 n) {
				float NdotTClamped = max(dot(n,t), 0.0);
				return NdotTClamped / (a * sqrt(1.0 - pow(NdotTClamped, 2.0)));
			}

			float G(float a, vec3 l, vec3 v, vec3 h, vec3 n) {
				float c1 = GBeckmannHelper(a, v, n);
				float c2 = GBeckmannHelper(a, l, n);

				float c12 = c1*c1;
				float c22 = c2*c2;
				float GV, GL;

				if(c1 < 1.6) {
					GV = (3.535 * c1 + 2.181 * c12) / (1.0 + 2.276*c1 + 2.577*c12);
				}
				else {
					GV = 1.0;
				}

				if(c2 < 1.6) {
					GL = (3.535 * c2 + 2.181 * c22) / (1.0 + 2.276*c2 + 2.577*c22);
				}
				else {
					GL = 1.0;
				}

				return GL*GV;
			}

		</script>


		<script id="GSchlick_Beckmann" type="x-shader/x-fragment">
			float G(float a, vec3 l, vec3 v, vec3 h, vec3 n) {
				float k = a * sqrt(2.0/M_PI);
				float NdotVClamped = dotClamped(n,v);
				float NdotLClamped = dotClamped(n,l);

				float GV = NdotVClamped / (NdotVClamped * (1.0-k) + k);
				float GL = NdotLClamped / (NdotLClamped * (1.0-k) + k);

				return GV*GL;
			}
		</script>


		<script id="fragmentShader_main" type="x-shader/x-fragment">
			void main()	{
		//		viewMatrix
		//		cameraPosition
				vec3 normal = normalize(vNormal.xyz);
				vec3 viewNormal = normalize(vViewNormal.xyz);
				vec3 viewDir = normalize(-vViewPosition);
				//vec3 lightDir = normalize(u_lightPos - vPosition).xyz;
				vec3 lightDir = normalize((viewMatrix * vec4(normalize(u_lightDir), 0.0)).xyz);
				vec3 halfVec = normalize(viewDir + lightDir);

				float diffuse = max(dot(normalize(u_lightDir), normal), 0.0);

				float NdotL= dot(viewNormal, lightDir);
				float NdotV= dot(viewNormal, viewDir);
				float NdotLClamped = max(NdotL, 0.0);
				float NdotVClamped = max(NdotV, 0.0);

				float alpha = u_roughness * u_roughness;

				float fresnel = pow((1.0 - u_fresnel) / (1.0 + u_fresnel), 2.0);

				float brdf_spec = F(fresnel, lightDir, halfVec) * G(alpha, lightDir, viewDir, halfVec, viewNormal) * N(alpha, viewNormal, halfVec) / (4.0 * NdotLClamped  * NdotVClamped );
				//float brdf_spec = F(fresnel, lightDir, halfVec) * GSchlick_Beckmann(alpha, lightDir, viewDir, halfVec, viewNormal) * NDFBeckmann(alpha, viewNormal, halfVec) / (4.0 * NdotLClamped  * NdotVClamped );

				vec3 specColor = M_PI * brdf_spec * u_lightColor * NdotLClamped;
				vec3 diffuseColor = u_diffuseColor.xyz * diffuse * (1.0 - F(fresnel, lightDir, halfVec));

				//gl_FragColor = vec4(vec3(brdf_spec, brdf_spec, brdf_spec), 1.0);
				gl_FragColor = vec4( diffuseColor +  specColor, 1.0);
			}
		</script>
		<script type="text/javascript" src="/~sijietian/pbr/main.js"></script>



	</body>
</html>
